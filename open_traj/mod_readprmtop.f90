MODULE mod_readprmtop
  USE mod_procedures
  USE mod_interfaces
  implicit none


  contains

  SUBROUTINE READTOP(infile,NATOM,NTYPES,NBONH,MBONA,NTHETH,MTHETA,NPHIH,MPHIA,NHPARM,NPARM,      &
                     NNB,NRES,NBONA,NTHETA,NPHIA,NUMBND,NUMANG,NPTRA,NATYP,NPHB,IFPERT,NBPER,     &
                     NGPER,NDPER,MBPER,MGPER,MDPER,IFBOX,NMXRS,IFCAP,IPTRES,       &
                     NSPM,NSPSOL,NATCAP,IPOL,OLDBETA,BOX,CUTCAP,XCAP,YCAP,ZCAP,RADIUS_SET,        &
                     ATOM_NAME,RESIDUE_LABEL,RESIDUES,RESID,AMBER_ATOM_TYPE,TREE_CHAIN_CLASSIFICATION,  &                     
                     CHARGE,MASS,BOND_FORCE_CONSTANT,BOND_EQUIL_VALUE,ANGLE_FORCE_CONSTANT,       &
                     ANGLE_EQUIL_VALUE,DIHEDRAL_FORCE_CONSTANT,DIHEDRAL_PERIODICITY,              &
                     DIHEDRAL_PHASE,SCEE_SCALE_FACTOR,SNCB_SCALE_FACTOR,SOLTY,LENNARD_JONES_ACOEF,&
                     LENNARD_JONES_BCOEF,HBOND_ACOEF,HBOND_BCOEF,HBCUT,RADII,POLARIZABILITY,      &                     
                     ATOMIC_NUMBER,ATOM_TYPE_INDEX,NUMBER_EXCLUDED_ATOMS,                         &
                     NONBONDED_PARM_INDEX,RESIDUE_POINTER,BONDS_INC_HYDROGEN,                     &
                     BONDS_WITHOUT_HYDROGEN,ANGLES_INC_HYDROGEN,                                  &
                     ANGLES_WITHOUT_HYDROGEN,DIHEDRALS_INC_HYDROGEN,                              &
                     DIHEDRALS_WITHOUT_HYDROGEN,EXCLUDED_ATOMS_LIST,JOIN_ARRAY,                   &
                     IROTAT,ATOMS_PER_MOLECULE)
 
    implicit none


    CHARACTER(LEN=*),INTENT(IN) :: infile

    CHARACTER(LEN=80) :: TMPREAD,FLAG
    CHARACTER(LEN=10) :: CURRFMT
    INTEGER :: i,io,b1,b2
    INTEGER :: NBRELEM

    ! .. Nbr of elements per section ..
    INTEGER,INTENT(OUT) :: NATOM,NTYPES,NBONH,MBONA,NTHETH,MTHETA,NPHIH,MPHIA,NHPARM,NPARM
    INTEGER,INTENT(OUT) :: NNB,NRES,NBONA,NTHETA,NPHIA,NUMBND,NUMANG,NPTRA,NATYP,NPHB
    INTEGER,INTENT(OUT) :: IFPERT,NBPER,NGPER,NDPER,MBPER,MGPER,MDPER,IFBOX,NMXRS,IFCAP
    INTEGER,INTENT(OUT) :: IPTRES,NSPM,NSPSOL,NATCAP,IPOL
    REAL   ,INTENT(OUT) :: OLDBETA,BOX(3),CUTCAP,XCAP,YCAP,ZCAP
    CHARACTER(LEN=80),INTENT(OUT) :: RADIUS_SET


    CHARACTER(LEN=4),ALLOCATABLE,INTENT(OUT) :: &
      ATOM_NAME(:),RESIDUE_LABEL(:),RESIDUES(:),AMBER_ATOM_TYPE(:),    &
      TREE_CHAIN_CLASSIFICATION(:)

    REAL,            ALLOCATABLE,INTENT(OUT) :: &
      CHARGE(:),MASS(:),BOND_FORCE_CONSTANT(:),BOND_EQUIL_VALUE(:),             &
      ANGLE_FORCE_CONSTANT(:),ANGLE_EQUIL_VALUE(:),DIHEDRAL_FORCE_CONSTANT(:),  &
      DIHEDRAL_PERIODICITY(:),DIHEDRAL_PHASE(:),SCEE_SCALE_FACTOR(:),           &
      SNCB_SCALE_FACTOR(:),SOLTY(:),LENNARD_JONES_ACOEF(:),                     &
      LENNARD_JONES_BCOEF(:),HBOND_ACOEF(:),HBOND_BCOEF(:),HBCUT(:),RADII(:),   &
      POLARIZABILITY(:)

    INTEGER,         ALLOCATABLE,INTENT(OUT) :: &
      ATOMIC_NUMBER(:),ATOM_TYPE_INDEX(:),NUMBER_EXCLUDED_ATOMS(:),             &
      NONBONDED_PARM_INDEX(:),RESIDUE_POINTER(:),RESID(:),BONDS_INC_HYDROGEN(:),         &
      BONDS_WITHOUT_HYDROGEN(:),ANGLES_INC_HYDROGEN(:),                         &
      ANGLES_WITHOUT_HYDROGEN(:),DIHEDRALS_INC_HYDROGEN(:),                     &
      DIHEDRALS_WITHOUT_HYDROGEN(:),EXCLUDED_ATOMS_LIST(:),JOIN_ARRAY(:),       &
      IROTAT(:),ATOMS_PER_MOLECULE(:)


    OPEN(10,FILE=infile,STATUS='OLD',ACTION='READ',IOSTAT=io)
    DO WHILE (io.GE.0)
      READ(10,'(A)',IOSTAT=io) TMPREAD
      IF (INDEX(TMPREAD,'%FLAG').NE.0) THEN

        ! Get flag name
        FLAG=TMPREAD(INDEX(TMPREAD,' ')+1:)

        ! Read format
        READ(10,'(A)',IOSTAT=io) TMPREAD
        CURRFMT=TMPREAD(INDEX(TMPREAD,'(')+1:INDEX(TMPREAD,')')-1)

        ! Extract nbr of elements per line
        IF (INDEX(CURRFMT,'I').NE.0) READ(CURRFMT(:INDEX(CURRFMT,'I')-1),*) NBRELEM
        IF (INDEX(CURRFMT,'E').NE.0) READ(CURRFMT(:INDEX(CURRFMT,'E')-1),*) NBRELEM
        IF (INDEX(CURRFMT,'a').NE.0) READ(CURRFMT(:INDEX(CURRFMT,'a')-1),*) NBRELEM

        ! Remake a usable format
        CURRFMT='('//TRIM(CURRFMT)//')'


        ! Read system info
        IF (TRIM(FLAG).EQ.'POINTERS') THEN
          READ(10,*,IOSTAT=io) NATOM,NTYPES,NBONH,MBONA,NTHETH,MTHETA,NPHIH,MPHIA,NHPARM,NPARM
          READ(10,*,IOSTAT=io) NNB,NRES,NBONA,NTHETA,NPHIA,NUMBND,NUMANG,NPTRA,NATYP,NPHB
          READ(10,*,IOSTAT=io) IFPERT,NBPER,NGPER,NDPER,MBPER,MGPER,MDPER,IFBOX,NMXRS,IFCAP
        END IF

        ! Allocate a vector whenever a flag is encountered
        IF (TRIM(FLAG).EQ.'ATOM_NAME') THEN
          ALLOCATE(ATOM_NAME(NATOM))
          CALL READVEC(ATOM_NAME,NATOM,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'CHARGE') THEN
          ALLOCATE(CHARGE(NATOM))
          CALL READVEC(CHARGE,NATOM,NBRELEM,CURRFMT)
          !CALL PRINTVEC(CHARGE)
        END IF

        IF (TRIM(FLAG).EQ.'ATOMIC_NUMBER') THEN
          ALLOCATE(ATOMIC_NUMBER(NATOM))
          CALL READVEC(ATOMIC_NUMBER,NATOM,NBRELEM,CURRFMT)
        END IF      

        IF (TRIM(FLAG).EQ.'MASS') THEN
          ALLOCATE(MASS(NATOM))
          CALL READVEC(MASS,NATOM,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'ATOM_TYPE_INDEX') THEN
          ALLOCATE(ATOM_TYPE_INDEX(NATOM))
          CALL READVEC(ATOM_TYPE_INDEX,NATOM,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'NUMBER_EXCLUDED_ATOMS') THEN
          ALLOCATE(NUMBER_EXCLUDED_ATOMS(NATOM))
          CALL READVEC(NUMBER_EXCLUDED_ATOMS,NATOM,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'NONBONDED_PARM_INDEX') THEN
          ALLOCATE(NONBONDED_PARM_INDEX(INT(NTYPES*NTYPES)))
          CALL READVEC(NONBONDED_PARM_INDEX,INT(NTYPES*NTYPES),NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'RESIDUE_LABEL') THEN
          ALLOCATE(RESIDUE_LABEL(NRES))
          CALL READVEC(RESIDUE_LABEL,NRES,NBRELEM,CURRFMT)
        END IF
 
        IF (TRIM(FLAG).EQ.'RESIDUE_POINTER') THEN
          ALLOCATE(RESIDUE_POINTER(NRES))
          CALL READVEC(RESIDUE_POINTER,NRES,NBRELEM,CURRFMT)

          ! Create the resid and resname column like in a pdb
          ALLOCATE(RESIDUES(NATOM)) ; ALLOCATE(RESID(NATOM))
          DO i=2,NRES
            b1=RESIDUE_POINTER(i-1) ; b2=RESIDUE_POINTER(i)-1
            RESIDUES(b1:b2)=RESIDUE_LABEL(i-1)
            RESID(b1:b2)=i-1
          END DO
          RESIDUES(b2+1:NATOM)=RESIDUE_LABEL(NRES)
          RESID(b2+1:NATOM)=NRES

          ! Discard the RESIDUE_POINTER array
          DEALLOCATE(RESIDUE_POINTER)
        END IF

        IF (TRIM(FLAG).EQ.'BOND_FORCE_CONSTANT') THEN
          ALLOCATE(BOND_FORCE_CONSTANT(NUMBND))
          CALL READVEC(BOND_FORCE_CONSTANT,NUMBND,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'BOND_EQUIL_VALUE') THEN
          ALLOCATE(BOND_EQUIL_VALUE(NUMBND))
          CALL READVEC(BOND_EQUIL_VALUE,NUMBND,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'ANGLE_FORCE_CONSTANT') THEN
          ALLOCATE(ANGLE_FORCE_CONSTANT(NUMANG))
          CALL READVEC(ANGLE_FORCE_CONSTANT,NUMANG,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'ANGLE_EQUIL_VALUE') THEN
          ALLOCATE(ANGLE_EQUIL_VALUE(NUMANG))
          CALL READVEC(ANGLE_EQUIL_VALUE,NUMANG,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'DIHEDRAL_FORCE_CONSTANT') THEN
          ALLOCATE(DIHEDRAL_FORCE_CONSTANT(NPTRA))
          CALL READVEC(DIHEDRAL_FORCE_CONSTANT,NPTRA,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'DIHEDRAL_PERIODICITY') THEN
          ALLOCATE(DIHEDRAL_PERIODICITY(NPTRA))
          CALL READVEC(DIHEDRAL_PERIODICITY,NPTRA,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'DIHEDRAL_PHASE') THEN
          ALLOCATE(DIHEDRAL_PHASE(NPTRA))
          CALL READVEC(DIHEDRAL_PHASE,NPTRA,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'SCEE_SCALE_FACTOR') THEN
          ALLOCATE(SCEE_SCALE_FACTOR(NPTRA))
          CALL READVEC(SCEE_SCALE_FACTOR,NPTRA,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'SNCB_SCALE_FACTOR') THEN
          ALLOCATE(SNCB_SCALE_FACTOR(NPTRA))
          CALL READVEC(SNCB_SCALE_FACTOR,NPTRA,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'SOLTY') THEN
          ALLOCATE(SOLTY(NATYP))
          CALL READVEC(SOLTY,NATYP,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'LENNARD_JONES_ACOEF') THEN
          ALLOCATE(LENNARD_JONES_ACOEF( INT((NTYPES * (NTYPES+1))/2) ))
          CALL READVEC(LENNARD_JONES_ACOEF,INT((NTYPES * (NTYPES+1))/2),NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'LENNARD_JONES_BCOEF') THEN
          ALLOCATE(LENNARD_JONES_BCOEF( INT((NTYPES * (NTYPES+1))/2) ))
          CALL READVEC(LENNARD_JONES_BCOEF,INT((NTYPES * (NTYPES+1))/2),NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'BONDS_INC_HYDROGEN') THEN
          ALLOCATE(BONDS_INC_HYDROGEN( INT(3*NBONH) ))
          CALL READVEC(BONDS_INC_HYDROGEN,INT(3*NBONH),NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'BONDS_WITHOUT_HYDROGEN') THEN
          ALLOCATE(BONDS_WITHOUT_HYDROGEN( INT(3*NBONA) ))
          CALL READVEC(BONDS_WITHOUT_HYDROGEN,INT(3*NBONA),NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'ANGLES_INC_HYDROGEN') THEN
          ALLOCATE(ANGLES_INC_HYDROGEN( INT(4*NTHETH) ))
          CALL READVEC(ANGLES_INC_HYDROGEN,INT(4*NTHETH),NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'ANGLES_WITHOUT_HYDROGEN') THEN
          ALLOCATE(ANGLES_WITHOUT_HYDROGEN( INT(4*NTHETA) ))
          CALL READVEC(ANGLES_WITHOUT_HYDROGEN,INT(4*NTHETA),NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'DIHEDRALS_INC_HYDROGEN') THEN
          ALLOCATE(DIHEDRALS_INC_HYDROGEN( INT(5*NPHIH) ))
          CALL READVEC(DIHEDRALS_INC_HYDROGEN,INT(5*NPHIH),NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'DIHEDRALS_WITHOUT_HYDROGEN') THEN
          ALLOCATE(DIHEDRALS_WITHOUT_HYDROGEN( INT(5*NPHIA) ))
          CALL READVEC(DIHEDRALS_WITHOUT_HYDROGEN,INT(5*NPHIA),NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'EXCLUDED_ATOMS_LIST') THEN
          ALLOCATE(EXCLUDED_ATOMS_LIST(NNB))
          CALL READVEC(EXCLUDED_ATOMS_LIST,NNB,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'HBOND_ACOEF') THEN
          ALLOCATE(HBOND_ACOEF(NPHB))
          CALL READVEC(HBOND_ACOEF,NPHB,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'HBOND_BCOEF') THEN
          ALLOCATE(HBOND_BCOEF(NPHB))
          CALL READVEC(HBOND_BCOEF,NPHB,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'HBCUT') THEN
          ALLOCATE(HBCUT(NPHB))
          CALL READVEC(HBCUT,NPHB,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'AMBER_ATOM_TYPE') THEN
          ALLOCATE(AMBER_ATOM_TYPE(NATOM))
          CALL READVEC(AMBER_ATOM_TYPE,NATOM,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'TREE_CHAIN_CLASSIFICATION') THEN
          ALLOCATE(TREE_CHAIN_CLASSIFICATION(NATOM))
          CALL READVEC(TREE_CHAIN_CLASSIFICATION,NATOM,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'JOIN_ARRAY') THEN
          ALLOCATE(JOIN_ARRAY(NATOM))
          CALL READVEC(JOIN_ARRAY,NATOM,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'IROTAT') THEN
          ALLOCATE(IROTAT(NATOM))
          CALL READVEC(IROTAT,NATOM,NBRELEM,CURRFMT)
        END IF

        ! .. IF BOX INFO IS DEFINED: ..
        IF (IFBOX.GT.0) THEN

          IF (TRIM(FLAG).EQ.'SOLVENT_POINTERS') THEN
            READ(10,*,IOSTAT=io) IPTRES,NSPM,NSPSOL
          END IF

          IF (TRIM(FLAG).EQ.'ATOMS_PER_MOLECULE') THEN
            ALLOCATE(ATOMS_PER_MOLECULE(NSPM))
            CALL READVEC(ATOMS_PER_MOLECULE,NSPM,NBRELEM,CURRFMT)
          END IF

          IF (TRIM(FLAG).EQ.'BOX_DIMENSIONS') THEN
            READ(10,*,IOSTAT=io) OLDBETA,BOX(:)
          END IF

        END IF

        ! .. IF WATER CAP IS PROVIDED: ..
        IF (IFCAP.NE.0) THEN

          IF (TRIM(FLAG).EQ.'CAP_INFO') THEN
            READ(10,*,IOSTAT=io) NATCAP
          END IF

          IF (TRIM(FLAG).EQ.'CAP_INFO2') THEN
            READ(10,*,IOSTAT=io) CUTCAP,XCAP,YCAP,ZCAP
          END IF

        END IF

        IF (TRIM(FLAG).EQ.'RADIUS_SET') THEN
          READ(10,*,IOSTAT=io) RADIUS_SET
        END IF

        IF (TRIM(FLAG).EQ.'RADII') THEN
          ALLOCATE(RADII(NATOM))
          CALL READVEC(RADII,NATOM,NBRELEM,CURRFMT)
        END IF

        IF (TRIM(FLAG).EQ.'IPOL') THEN
          READ(10,*,IOSTAT=io) IPOL
        END IF

        ! .. IF POLARISABLE FF IS USED: ..
        IF (IPOL.NE.0) THEN

          IF (TRIM(FLAG).EQ.'POLARIZABILITY') THEN
            ALLOCATE(POLARIZABILITY(NATOM))
            CALL READVEC(POLARIZABILITY,NATOM,NBRELEM,CURRFMT)
          END IF

        END IF

      END IF

    END DO
    CLOSE(10)

  END SUBROUTINE READTOP

END MODULE mod_readprmtop